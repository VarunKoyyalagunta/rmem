open import Pervasives
open import Assert_extra
open import Sail_impl_base
open import Utils
open import Events
open import Fragments
open import Params
open import Isa




let reg_from_data (data: registerdata) (name: reg_base_name) : maybe reg_name =
  Maybe.bind (List.lookup name data) $ fun (direction, width, start, _) ->
  Just (Reg name start width direction)

let reg_field_from_data (data: registerdata) (name: reg_base_name) (field_name: string) : maybe reg_name =
  Maybe.bind (List.lookup name data) $ fun (direction, width, start, fields) ->
  Maybe.bind (List.lookup field_name fields) $ fun slice ->
  let norm (d: direction) (start: nat) (i, j) = (* see interp_inter_imp.lem extern_slice *)
    match d with
    | D_increasing -> (i, j)
    | D_decreasing ->
        let i' = start - i in
        let j' = (i - j) + i' in
        (i', j')
    end
  in
  Just (Reg_field name start direction field_name (norm direction start slice))

let fixed_reg (data: registerdata) value (name: reg_base_name) =
  let reg = Maybe_extra.fromJust (reg_from_data data name) in
  (reg, register_value_for_reg_of_integer reg value)

let fixed_reg_field (data: registerdata) value (name: reg_base_name) (field_name: string) =
  let reg = Maybe_extra.fromJust (reg_field_from_data data name field_name) in
  (reg, register_value_for_reg_of_integer reg value)





let reassemble_register_value (r: reg_name) (mfs: list (maybe_value_fragment bit_lifted))
    : maybe register_value
  =
  value_of_maybe_value_fragments mfs
  $> Maybe.map (register_value_for_reg r)


let register_state_zero
    (register_data:registerdata)
    (tid: thread_id)
    (rbn: reg_base_name)
    : register_value =
  let (dir,width,start_index,_) =
    ensure_just (List.lookup rbn register_data)
      $ "register_state_zero lookup failed (" ^ rbn ^ ") looking at " ^ show (List.length register_data) ^ " registers."
  in
  register_value_zeros dir width start_index


let is_nia_register isa (reg: reg_name) : bool =
  register_base_name isa.nia_reg = register_base_name reg

let is_cia_register isa (reg: reg_name) : bool =
  register_base_name isa.cia_reg = register_base_name reg

(* 'is_pseudo_register params reg' is true iff 'reg' is a pseudo register *)
let is_pseudo_register isa : (reg_name -> bool) =
  let (fixed_pseudo_regs, _) = List.unzip isa.fixed_pseudo_registers in
  (fun (reg: reg_name) ->
      is_nia_register isa reg ||
      is_cia_register isa reg ||
      exists (reg' MEM fixed_pseudo_regs).
        register_base_name reg' = register_base_name reg)


let non_pseudo_registers isa (regs: set reg_name) : set reg_name =
  let is_pseudo_register' = is_pseudo_register isa in
  {reg | forall (reg IN regs) | not (is_pseudo_register' reg)}


let pseudo_register_value isa cia_address reg =
  let pseudo_register_values =
    let cia_reg = isa.cia_reg in
    let nia_reg = isa.nia_reg in
    (cia_reg, register_value_of_address cia_address (direction_of_reg_name cia_reg)) ::
      (nia_reg, register_value_of_address (address_of_integer ((integer_of_address cia_address)+4)) (direction_of_reg_name nia_reg)) :: (* FIXME HACK *)
      isa.fixed_pseudo_registers
  in

  let (needed, found) =
    match_maybe_value_fragments
      [slice_of_reg_name reg]
      [(slice_of_reg_name reg', Just rv.rv_bits)
          | forall ((reg', rv) MEM pseudo_register_values)
          | register_base_name reg' = register_base_name reg]
      []
  in

  let () = ensure (needed = [])
    $ "missing pseudo register slices (" ^ show reg ^ " " ^ show needed ^ ")"
  in

  ensure_just (reassemble_register_value reg found) "could not reconstruct pseudo register"




(** Dependency calculation *)
type register_read_source =
  | RRS_instruction of ioid * list reg_name * list (maybe_value_fragment bit_lifted)
  | RRS_initial_state of list (maybe_value_fragment bit_lifted)
  | RRS_pseudoregister
(* the lists of maybe_value_fragments above are recorded only for the sake of constructing axiomatic executions for the user interface *)

(* list with the most recent at the head *)
type register_read_sources = list register_read_source

type register_write_dependency =
    | RWD_reg_write of ioid * list reg_name (* a po-previous instruction and register writes
                                            that feed a register read that was performed
                                            before the register write *)
    | RWD_mem_read (* indicates the instruction performed a memory read
                   before the register write *)
